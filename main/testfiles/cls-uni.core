
 Prefix(rdf  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>)
 Prefix(rdfs <http://www.w3.org/2000/01/rdf-schema#>)
 Prefix(owl  <http://www.w3.org/2002/07/owl#>)
 Prefix(xsd  <http://www.w3.org/2001/XMLSchema#>)
 Prefix(rif  <http://www.w3.org/2007/rif#>)
 Prefix(func <http://www.w3.org/2007/rif-builtin-function#>)
 Prefix(pred <http://www.w3.org/2007/rif-builtin-predicate#>)
 Prefix(dc   <http://purl.org/dc/terms/>)

(* <#cls-uni> *)
 Forall ?y ?c ?l ?ci (
   ?y[rdf:type->?c] :- And (
		 _checkUnionOf(?c ?l)
		 ?l[rdf:first->?ci]
     ?y[rdf:type->?ci] ))

 Forall ?c ?l (
	 _checkUnionOf(?c ?l) :- ?c[owl:unionOf->?l] )

 Forall ?c ?l ?r (
	 _checkUnionOf(?c ?l) :- And(
		 _checkUnionOf(?c ?r)
		 ?r[rdf:rest->?l]
		 Not(?l = rdf:nil) ))

(* <#scm-uni> *)
 Forall ?c ?ci ?l (
   ?ci[rdfs:subClassOf->?c] :- And (
		 _checkUnionOf(?c ?l)
		 ?l[rdf:first->?ci] ))
 
