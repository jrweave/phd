Prefix(rdf  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>)
Prefix(rdfs <http://www.w3.org/2000/01/rdf-schema#>)
Prefix(owl  <http://www.w3.org/2002/07/owl#>)
Prefix(xsd  <http://www.w3.org/2001/XMLSchema#>)
Prefix(rif  <http://www.w3.org/2007/rif#>)
Prefix(func <http://www.w3.org/2007/rif-builtin-function#>)
Prefix(pred <http://www.w3.org/2007/rif-builtin-predicate#>)
Prefix(dc   <http://purl.org/dc/terms/>)

#PRAGMA REPLICATE And(?p[rdfs:domain -> ?c])
#PRAGMA REPLICATE And(?p[rdfs:range -> ?c])
#PRAGMA REPLICATE And(?p1[rdfs:subPropertyOf -> ?p2] Not(External(pred:list-contains(List(rdfs:domain rdfs:range rdfs:subPropertyOf rdfs:subClassOf) ?p2))))
#PRAGMA REPLICATE And(?c1[rdfs:subClassOf -> ?c2])

(* <#prp-dom> *)
Forall ?p ?c ?x ?y (
   ?x[rdf:type->?c] :- And(
       ?p[rdfs:domain->?c]
       ?x[?p->?y]  ))

(* <#prp-rng> *)
Forall ?p ?c ?x ?y (
   ?y[rdf:type->?c] :- And(
       ?p[rdfs:range->?c]
       ?x[?p->?y]  ))

(* <#cax-sco> *)
Forall ?x ?c1 ?c2 (
   ?x[rdf:type->?c2] :- And(
       ?c1[rdfs:subClassOf->?c2]
       ?x[rdf:type->?c1]  ))

(* <#scm-sco> *)
Forall ?c1 ?c2 ?c3 (
   ?c1[rdfs:subClassOf->?c3] :- And(
       ?c1[rdfs:subClassOf->?c2]
       ?c2[rdfs:subClassOf->?c3]  ))

(* <#scm-spo> *)
Forall ?p3 ?p2 ?p1 (
   ?p1[rdfs:subPropertyOf->?p3] :- And(
       ?p1[rdfs:subPropertyOf->?p2]
       ?p2[rdfs:subPropertyOf->?p3]  ))

(* <#prp-spo1> *)
Forall ?x ?y ?p2 ?p1 (
   ?x[?p2->?y] :- And(
       ?p1[rdfs:subPropertyOf->?p2]
			 Not(External(pred:list-contains(List(rdfs:domain rdfs:range rdfs:subPropertyOf rdfs:subClassOf) ?p2)))
       ?x[?p1->?y]  ))

